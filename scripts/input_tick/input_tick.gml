function input_tick()
{
    var _any_changed = false;
    
    global.__input_frame++;
    
    #region Mouse
    
    var _mouse_x = 0;
    var _mouse_y = 0;
    
    switch(INPUT_MOUSE_MODE)
    {
        case 0:
            _mouse_x = device_mouse_x(0);
            _mouse_y = device_mouse_y(0);
        break;
        
        case 1:
            _mouse_x = device_mouse_x_to_gui(0);
            _mouse_y = device_mouse_y_to_gui(0);
        break;
        
        case 2:
            _mouse_x = device_mouse_raw_x(0);
            _mouse_y = device_mouse_raw_y(0);
        break;
        
        default:
            __input_error("Invalid mouse mode (", INPUT_MOUSE_MODE, ")");
        break;
    }
    
    global.__input_mouse_moved = (point_distance(_mouse_x, _mouse_y, global.__input_mouse_x, global.__input_mouse_y) > INPUT_MOUSE_MOVE_DEADZONE);
    global.__input_mouse_x = _mouse_x;
    global.__input_mouse_y = _mouse_y;
    
    //Windows mouse extensions
    global.__input_tap_click = false;
    if (os_type == os_windows)
    {
        //Track clicks from touchpad and touchscreen taps (system-setting dependent)
        //N.B. Fix *not* needed in UWP
        global.__input_tap_presses  += device_mouse_check_button_pressed( 0, mb_left);
        global.__input_tap_releases += device_mouse_check_button_released(0, mb_left);
    
        //Resolve press/release desync (where press failed to register on same frame as release)
        if (global.__input_tap_releases >= global.__input_tap_presses)
        {
            global.__input_tap_click    = (global.__input_tap_releases > global.__input_tap_presses);
            global.__input_tap_presses  = 0;
            global.__input_tap_releases = 0;
        }
    }
    
    #endregion
    
    #region Keyboard

    if (__INPUT_KEYBOARD_SUPPORT) 
    {
        //Set keyboard string
        var _string = keyboard_string;
        var _prev_string = global.__input_keyboard_prev_string;
        if ((_string == "") && (string_length(_prev_string) > 1))
        {
             //Revert if overflowing
            _string = _prev_string;
        }
        
        //Set input string
        if (global.__input_async_id == undefined)
        {
            input_string_set(_string);
            global.__input_string = keyboard_string;
            if (os_type == os_android) 
            {
                //Trim leading space on Android
                global.__input_string = string_delete(global.__input_string, 1, 1);
            }
        }
    
        //Unstick
        if (keyboard_check(vk_anykey))
        {
            var _platform = os_type;
            if (__INPUT_ON_WEB && __INPUT_ON_APPLE)
            {
                _platform = "apple_web";
            }

            switch (_platform)
            {
                case os_windows:
                case os_uwp:
                    if (keyboard_check(vk_alt) && keyboard_check_pressed(vk_space))
                    {
                        //Unstick Alt Space
                        keyboard_key_release(vk_alt);
                        keyboard_key_release(vk_space);
                        keyboard_key_release(vk_lalt);
                        keyboard_key_release(vk_ralt);
                    }
                break;
            
                case "apple_web": //This case applies on iOS, tvOS, and MacOS
                    if (keyboard_check_released(92) || keyboard_check_released(93))
                    {
                        //Meta release sticks every key pressed during hold
                        //This is "the nuclear option", but the problem is severe
                        var _i = 8;
                        repeat(247)
                        {
                            keyboard_key_release(_i);
                            _i++;
                        }
                    }
                break;
                
                case os_macosx:
                    //Unstick control key double-ups
                    if (keyboard_check_released(vk_control))
                    {
                        keyboard_key_release(vk_lcontrol);
                        keyboard_key_release(vk_rcontrol);
                    }
            
                    if (keyboard_check_released(vk_shift))
                    {
                        keyboard_key_release(vk_lshift);
                        keyboard_key_release(vk_rshift);
                    }
            
                    if (keyboard_check_released(vk_alt))
                    {
                        keyboard_key_release(vk_lalt);
                        keyboard_key_release(vk_ralt);
                    }
            
                    //Unstick Meta
                    //Weird, but seems to be the best way to unstick without spoiling normal operation
                    if (keyboard_check_released(vk_meta1))
                    {
                        keyboard_key_release(vk_meta2);
                    }
                    else if (keyboard_check_released(vk_meta2) && keyboard_check(vk_meta1))
                    {
                        keyboard_key_release(vk_meta1);
                    }
                break;
            }
        }
    }
    
    #endregion
    
    #region Update gamepads
    
    //Expand dynamic device count
    var _device_change = max(0, gamepad_get_device_count() - array_length(global.__input_gamepads))
    repeat(_device_change) array_push(global.__input_gamepads, undefined);
    
    var _g = 0;
    repeat(array_length(global.__input_gamepads))
    {
        var _gamepad = global.__input_gamepads[_g];
        if (is_struct(_gamepad))
        {
            if (gamepad_is_connected(_g))
            {
                if (os_type == os_switch)
                {
                    //When L+R assignment is used to pair two gamepads we won't see a normal disconnection/reconnection
                    //Instead we have to check for changes in the description to see if state has changed
                    if (_gamepad.description != gamepad_get_description(_g))
                    {
                        _gamepad.discover();
                    }
                    else
                    {
                        _gamepad.tick();
                    }
                }
                else
                {
                    _gamepad.tick();
                }
            }
            else
            {
                //Remove our gamepad handler
                __input_trace("Gamepad ", _g, " disconnected");
                
                global.__input_gamepads[@ _g] = undefined;
                
                //Also report gamepad changes for any active players
                var _p = 0;
                repeat(INPUT_MAX_PLAYERS)
                {
                    with(global.__input_players[_p])
                    {
                        if ((gamepad == _gamepad) && (source == INPUT_SOURCE.GAMEPAD))
                        {
                            __input_trace("Player ", _p, " gamepad disconnected");
                            
                            source = INPUT_SOURCE.NONE;
                            _any_changed = true;
                        }
                    }
                    
                    ++_p;
                }
            }
        }
        else
        {
            if (gamepad_is_connected(_g))
            {
                __input_trace("Gamepad ", _g, " connected");
                __input_trace("New gamepad = \"", gamepad_get_description(_g), "\", GUID=\"", gamepad_get_guid(_g), "\"");
                
                global.__input_gamepads[@ _g] = new __input_class_gamepad(_g);
            }
        }
        
        ++_g;
    }
    
    #endregion
    
    var _p = 0;
    repeat(INPUT_MAX_PLAYERS)
    {
        global.__input_players[_p].tick();
        ++_p;
    }
    
    return _any_changed;
}
